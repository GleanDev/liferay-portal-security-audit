buildscript {
    dependencies {
        classpath group: "com.liferay", name: "com.liferay.gradle.plugins",
                version: "14.0.158"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:4.2.1.3168"
    }

    repositories {
        mavenCentral()
        maven {
            url "https://repository-cdn.liferay.com/nexus/content/groups/public"
        }
    }
}

subprojects {
    buildscript {
        repositories {
            maven {
                url "https://repository-cdn.liferay.com/nexus/content/groups/public"
            }
        }
    }

    apply plugin: "com.liferay.plugin"
    apply plugin: "maven-publish"

    plugins.withId("java") {
        sourceCompatibility = "1.8"
    }

    repositories {
        maven {
            url "https://repository-cdn.liferay.com/nexus/content/groups/public"
        }
    }

    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/amusarra/liferay-portal-security-audit")
                credentials {
                    username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                    password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                }
            }
        }
        publications {
            gpr(MavenPublication) {
                from(components.java)
            }
        }
    }
}

apply plugin: "org.sonarqube"

sonar {
    properties {
        property "sonar.projectKey", "amusarra_liferay-portal-security-audit"
        property "sonar.organization", "amusarra-github"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}